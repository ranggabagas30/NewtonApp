apply plugin: 'com.android.application'

// Manifest version information!
def minAPIlevel = rootProject.ext.minSdkVersion
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.newtonapp.customer"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode minAPIlevel * 1000000 + versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "newtonapp-customer" + "-v" + "(" + versionName + ")")
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            applicationVariants.all { variant ->
                renameAPK(variant)
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                renameAPK(variant)
            }
        }
    }

    flavorDimensions "user_role"

    productFlavors {
        customer {
            applicationId 'com.newtonapp.customer'
            dimension "user_role"
        }
        technician {
            applicationId 'com.newtonapp.technician'
            dimension "user_role"
            versionMajor = 1
            versionMinor = 0
            versionPatch = 0
            versionCode minAPIlevel * 1000000 + versionMajor * 10000 + versionMinor * 100 + versionPatch
            versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

def renameAPK(variant) {
    variant.outputs.all { output ->
        def formattedDate = new Date().format('ddMMYY')
        outputFileName = "${variant.name}-${variant.versionName}-${formattedDate}.apk"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha09'

    // firebase integration
    implementation 'com.google.firebase:firebase-core:16.0.5'
    implementation 'com.google.firebase:firebase-ml-vision:18.0.1'

    // Room components
    implementation 'androidx.room:room-runtime:2.2.0-alpha02'
    annotationProcessor 'androidx.room:room-compiler:2.2.0-alpha02'

    // Lifecycle components
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-alpha03'
    annotationProcessor 'androidx.lifecycle:lifecycle-compiler:2.2.0-alpha03'

    // permissions
    implementation 'pub.devrel:easypermissions:3.0.0'

    // Networking
    // Retrofit http://square.github.io/retrofit
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0' // interceptor
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    // // retrofit2 rxjava2 adapter

    // reactive
    // rxAndroid 2.0 to subscribe on AndroidSchedulers class
    // https://github.com/ReactiveX/RxAndroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10' // rxJava2

    // custom shape imageview
    implementation 'com.github.siyamed:android-shape-imageview:0.9.+@aar'

    // svg support
    implementation 'com.pixplicity.sharp:library:1.1.0'
    implementation 'com.caverock:androidsvg-aar:1.4'

    // tableView
    implementation 'com.evrencoskun.library:tableview:0.8.8'

    // timelineView
    //StickyTimeLine v0.0.20 and above only supports projects that have been migrated to androidx.
    implementation 'com.github.sangcomz:StickyTimeLine:v0.0.20'

    // persistent
    implementation 'com.pixplicity.easyprefs:library:1.9.0'

    // debugging
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

apply plugin: 'com.google.gms.google-services'
